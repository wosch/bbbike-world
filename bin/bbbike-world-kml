#!/usr/local/bin/perl
# Copyright (c) 2009-2011 Wolfram Schneider, http://bbbike.org
#
# bbbike-world-kml - create an Google Earth KML file for all BBBike @ World cities

use lib '.';
use lib './world/bin';
use BBBikeWorldDB;

use strict;
use warnings;

sub usage () {
    <<EOF;
usage: $0 bbbike-db

EOF
}

sub extract_coords {
    my $database = shift;

    my $db = BBBikeWorldDB->new( 'database' => $database );

    my @data;
    foreach my $city ( $db->list_cities ) {
        my $c = $db->city->{$city};

        my ( $x1, $y1, $x2, $y2 ) = split( /\s+/, $c->{"coord"} );

        my $x = ( $x2 - $x1 ) / 2 + $x1;
        my $y = ( $y2 - $y1 ) / 2 + $y1;

        next if $city eq 'berlin';

        my $name = $db->select_city_name( $city, ( $c->{"name"} || $city ), 'en' );
        my $lang = "en";
        push( @data, [ $city, $name, $lang, $x, $y ] );
    }
    return @data;
}

sub bbbike2kml_header {
    print <<EOF;
<?xml version="1.0" encoding="UTF-8"?>
<kml xmlns="http://earth.google.com/kml/2.2">
<Document>
  <name>BBBike @ World - a Cycle Route Planner</name>
  <description><![CDATA[List of all Cities supported by <a href="http://www.bbbike.org/">BBBike @ World</a>.]]></description>
  <Style id="style1">
    <IconStyle>
      <Icon>
        <href>http://maps.google.com/mapfiles/ms/micons/pink-pushpin.png</href>
      </Icon>
    </IconStyle>
  </Style>
EOF
}

sub bbbike2kml_footer {
    print <<EOF;
</Document>
</kml>
EOF
}

sub bbbike2kml_placemarks {
    my @data = @_;

    foreach my $c ( sort { $a->[0] cmp $b->[0] } @data ) {
        my ( $city, $name, $lang, $x, $y ) = @$c;

        my $bbbike_url = "http://www.bbbike.org/$city/";

        print <<EOF;
  <Placemark>
    <name>$name</name>
    <description><![CDATA[Start <span style="color:rgb(0, 0, 238);text-decoration:underline"><a href="$bbbike_url">BBBike @ $name</a></span>]]></description>
    <styleUrl>#style1</styleUrl>
    <Point>
      <coordinates>$x,$y,0.000000</coordinates>
    </Point>
  </Placemark>
EOF
    }
}

sub bbbike2kml {
    my @data = @_;

    &bbbike2kml_header;
    &bbbike2kml_placemarks(@data);
    &bbbike2kml_footer;
}

my $db = $ARGV[0];
die &usage if !$db;

my @data = &extract_coords($db);
&bbbike2kml(@data);

