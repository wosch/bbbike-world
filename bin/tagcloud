#!/usr/local/bin/perl

use HTML::TagCloud 0.34;
use Getopt::Long;
use strict;
use warnings;

my $debug         = 1;      # 0: quiet, 1: normal, 2: verbose
my $selected_area = 'de';
my $level         = 12;

sub usage () {
    <<EOF;
usage: $0 [--debug={0..2}] [--level=number ] [ --area=area ] < population.csv

--debug=0..2    debug option
--area=		de | eu | other
--level=16
EOF
}

# several tagclouds on one page
sub id2class {
    my $data = shift;
    $data =~ s,<div id="htmltagcloud">,<div class="htmltagcloud">,;
    return $data;
}

GetOptions(
    "debug=i" => \$debug,
    "area=s"  => \$selected_area,
    "level=i" => \$level,
) or die usage;

my $cloud = HTML::TagCloud->new( levels => $level );

while (<>) {
    chomp;
    next if /^\s*#/;

    my ( $city, $name, $lang, $area, $coord, $population ) = split(/:/);

    $lang       = "de"  if !$lang;
    $area       = "de"  if !$area;
    $population = 1     if $population < 1;
    $name       = $city if !$name;

    next if $area ne $selected_area;

    my $link = "cgi/$city" . ( $lang eq 'de' ? '' : ".$lang" ) . '.cgi';
    $cloud->add( $name, $link, $population );
}

print &id2class( $cloud->html ), "\n";

