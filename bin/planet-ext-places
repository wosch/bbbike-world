#!/usr/local/bin/perl 
# Copyright (c) 2009-2011 Wolfram Schneider, http://bbbike.org
#
# planet-ext-places - extract street names from an OpenStreetMap
#	XML dump. The output format is:
#
#		streetname <tab> lon,lat
#
# you can use this script to build an OpenSearch suggestion database
#

use XML::LibXML::Reader;
use IO::File;
use File::Temp;
use Data::Dumper;
use Getopt::Long;
use Search::Dict;

use strict;
use warnings;

use vars qw($VERSION);
$VERSION = 0.1;

# unicode sort(1) mess
$ENV{'LANG'} = 'C';

my $debug = 1;    # 0: quiet, 1: normal, 2: verbose
my $help;

my $xmlparser;

sub usage () {
    <<EOF;
usage: $0 [ options ] osm_files ....

--debug=0..2	  debug option, default: $debug
osmfile ...       Files downloaded by downloadosm,
                  or directory containing .osm files
EOF
}

sub extract_places {
    my $reader = shift;

    my $flag              = 0;
    my $parse_street_flag = 0;

    my @todo;

    while ( $flag || $reader->nextElement == 1 ) {
        last if !$reader->name;

        $flag = 0;

        if ( $reader->name eq 'node' ) {
            my $id  = $reader->getAttribute('id');
            my $lat = $reader->getAttribute('lat');
            my $lon = $reader->getAttribute('lon');

            #print $fd_nodes $id, "\t", $lon, ",", $lat, "\n";

            my %tag;
            while ( $reader->nextElement == 1 ) {
                my $node_name = $reader->name;

                if ( $node_name eq 'tag' ) {
                    $tag{ $reader->getAttribute('k') } =
                      $reader->getAttribute('v');
                }
                else {

                    # already read the next node
                    $flag = 1;
                    last;
                }
            }

            if ( $tag{'place'} ) {
                my $name = $tag{'name'} || '';

                # city name in other languages
                my %citynames;
                while ( my ( $key, $val ) = each %tag ) {
                    if ( $key =~ /^name:(\w+)$/ && $val ) {
                        push( @{ $citynames{$val} }, $1 );
                    }
                }

                # sort languages by name: city [de,en,zu]
                foreach my $key ( sort keys %citynames ) {
                    my $val = $citynames{$key};
                    $name .= " / $key [" . join( ',', sort @{$val} ) . ']';
                }

                $name .= ' [ZIP ' . $tag{'postal_code'} . ']'
                  if exists $tag{'postal_code'} && $name;

                my $is_in = $tag{'openGeoDB:is_in'} || $tag{'is_in'};
                $name .= ' [is in: ' . $is_in . ']' if $is_in;

                $name =~ s/^\s+//;
                $name =~ s/\s+$//;
                $name =~ s/\t/ /g;
                $name =~ s/  +/ /g;
                $name =~ s/^\s*$//;

                if ($name) {
                    print $name, "\t", "$lon,$lat", "\n";
                }
            }
        }
    }
}

######################################################################
#
# main
#

GetOptions(
    "help"    => \$help,
    "debug=i" => \$debug,
) or die usage;

my @osm_files = @ARGV;
die &usage if $help or !@osm_files;

my $reader;
my $fh;
for my $osm_file (@osm_files) {

    warn "Parse osm db $osm_file ...\n" if $debug >= 2;

    if ( $osm_file =~ m{\.osm\.bz2$} ) {
        open $fh, "-|", "osm_bzcat", $osm_file
          or die "Can't run bunzip2 on $osm_file: $!";
    }
    elsif ( $osm_file =~ m{\.osm\.gz$} ) {
        open $fh, "-|", "osm_zcat", $osm_file
          or die "Can't run gzip on $osm_file: $!";
    }
    elsif ( $osm_file =~ m{\.osm$} ) {
        open $fh, $osm_file
          or die "Can't open $osm_file: $!";
    }
    elsif ( $osm_file eq '-' ) {
        $fh = \*STDIN;
    }
    else {
        warn "Unknown file extenstion: $osm_file\n";
        warn "Expect *.osm.gz, *.osm.bz2, *.osm or '-'\n";
        exit(1);
    }

    binmode( $fh, ":utf8" );

    $reader = XML::LibXML::Reader->new( IO => $fh );

    # check if osm file
    $reader->nextElement;
    if ( $reader->name ne 'osm' ) {
        die
"The file '$osm_file' is not starting with a <osm> tag, probably not an osm file?";
    }

    binmode( \*STDOUT, ":utf8" );

    extract_places($reader);
}

