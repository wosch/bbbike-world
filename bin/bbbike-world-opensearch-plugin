#!/usr/local/bin/perl
# Copyright (c) 2009-2011 Wolfram Schneider, http://bbbike.org
#
# bbbike-world-opensearch-plugin - create an opensearch plugin for  BBBike @ World cities
#

use IO::File;

use lib '.';
use lib './world/bin';
use BBBikeWorldDB;

use strict;
use warnings;

binmode \*STDIN,  ":utf8";
binmode \*STDOUT, ":utf8";

my $debug = $ENV{DEBUG};
my $homepage = $ENV{BBBIKE_HOMEPAGE} || 'http://www.bbbike.org';

sub usage () {
    <<EOF;
usage: $0 directory bbbike-db

EOF
}

sub extract_coords {
    my $db = shift;

    my @data;

    warn "Open $db\n" if $debug;
    my $fh = new IO::File $db, "r" or die "open: $!\n";
    while (<$fh>) {
        chomp;
        s/^\s+//;
        next if /^#/ || $_ eq "";

        my ( $city, $name, $lang, $local_lang, $area, $coord, $population ) =
          split(/:/);
        my ( $x1, $y1, $x2, $y2 ) = split( /\s+/, $coord );

        my $x = ( $x2 - $x1 ) / 2 + $x1;
        my $y = ( $y2 - $y1 ) / 2 + $y1;

        next if $city eq 'dummy';
        $name = $city if !$name;

        push( @data, [ $city, $name, $lang, $x, $y ] );

    }
    close $fh;

    return @data;
}

sub create_opensearch_file {
    my %args = @_;

    my $file = $args{'file'} or die "no file given\n";
    my $city = $args{'city'} or die "no city given\n";
    my $name       = $args{'name'}       || $city;
    my $lang       = $args{'lang'}       || "en";
    my $queryparam = $args{'queryparam'} || "q";
    my $scriptname = $args{'scriptname'};

    warn "Create opensearch file $file\n" if $debug;
    my $fh = new IO::File $file, "w" or die "open '$file': $!\n";
    binmode $fh, ":utf8";

    my $lang_prefix = $lang eq 'en' ? "$lang/" : "";

    print $fh <<EOF;
<?xml version="1.0"?>
<OpenSearchDescription xmlns="http://a9.com/-/spec/opensearch/1.1/" xml:lang="en">
  <ShortName>BBBike @ $city ($lang)</ShortName>
  <LongName>BBBike @ <![CDATA[$name]]></LongName>
  <Description>BBBike @ World</Description>
  <InputEncoding>UTF-8</InputEncoding>
  <Url template="$homepage/$lang_prefix$city/?sourceid=opensearch&amp;$queryparam={searchTerms}" type="text/html" />
  <Url type="application/x-suggestions+json" template="$homepage/cgi/api.cgi?sourceid=opensearch&amp;action=opensearch&amp;city=$city&amp;namespace=0&amp;search={searchTerms}"/>
  <Developer>Wolfram Schneider, Slaven Rezi&#x0107;</Developer>
  <Contact>http://www.bbbike.org/</Contact>
  <Image height="16" type="image/png" width="16">$homepage/images/srtbike16.gif</Image>
  <Language>$lang</Language>
</OpenSearchDescription>
EOF

    close $fh;

}

sub bbbike2opensearch {
    my %args   = @_;
    my $dir    = $args{'dir'};
    my $db     = $args{'db'};
    my @cities = @{ $args{'cities'} };

    for my $city (@cities) {
        my $c = $db->city->{$city};

        my @lang = "en";
        push( @lang, $c->{"local_lang"} ) if $c->{"local_lang"};

        for my $lang (@lang) {
            my $xml_file =
              "$dir/$city" . ( $lang eq 'en' ? ".$lang" : "" ) . ".xml";
            my $city_name =
              $db->select_city_name( $c->{"city"},
                ( $c->{"name"} || $c->{"city"} ), $lang );

            create_opensearch_file(
                'file'       => $xml_file,
                'city'       => $city,
                'name'       => $city_name,
                'lang'       => $lang,
                'queryparam' => 'start',
            );
        }
    }
}

sub bbbike2opensearch_streets {
    my %args   = @_;
    my $dir    = $args{'dir'};
    my @cities = @{ $args{'cities'} };
    my $lang   = $args{'lang'} || "en";
    my $db     = $args{'database'};

    for my $city (@cities) {
        my $c = $db->city->{$city};

        #my $city_lang = $lang eq '' ? $c->{"local_lang"} : $city_lang;

        my $xml_file = "$dir/$city" . ( $lang ? ".$lang" : "" ) . ".xml";
        my $name =
          $db->select_city_name( $c->{"city"}, ( $c->{"name"} || $c->{"city"} ),
            $lang );

        create_opensearch_file(
            'file'       => $xml_file,
            'city'       => $city,
            'name'       => $name,
            'lang'       => $lang,
            'queryparam' => 'search',
        );
    }
}

######################################################################
# main
#
my $streets;
my $db_file = 'world/etc/cities.csv';
if ( $ARGV[0] eq '--streets' ) {
    $streets = 1;
    shift @ARGV;
}
my ( $dir, @cities ) = @ARGV;
die &usage if !scalar(@cities) || !$dir;

my $database = BBBikeWorldDB->new( 'database' => $db_file );

if ( !$streets ) {
    &bbbike2opensearch(
        'dir'    => $dir,
        'cities' => \@cities,
        'db'     => $database
    );
}
else {

# &bbbike2opensearch_streets( 'dir'        => $dir, 'cities'       => \@cities, 'db' => $database,);
}

